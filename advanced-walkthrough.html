<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
<title>Abella: Advanced Walkthrough</title>
<link href="style.css" rel="stylesheet" type="text/css" />
<link rel="icon" href="images/favicon.ico" type="image/x-icon" />
<link rel="shortcut icon" href="images/favicon.ico"
        type="image/x-icon" />
</head>

<body>

<div id="logo-small">
<a href="index.html">
<img src="images/logo-small.png"/>
</a>
</div>

<div class="section">
<h1 id="main">Advanced Walkthrough: Type Uniqueness</h1>
<br/>
<p>
In this walkthrough you will learn...
<ul>
<li>How to use lemmas</li>
<li>How to state and use definitions</li>
</ul>
</p>
</div>

<div class="section">
<h1>Introduction</h1>

<p>
This advanced walkthrough assumes you have finished the
<a href="walkthrough.html">beginner's walkthrough</a> and are familiar
with the following tactics: <code>induction</code>,
<code>intros</code>, <code>case</code>, <code>apply</code>, and
<code>search</code>.
</p>

<p>
This walkthrough is concerned with the simply-typed lambda calculus.
Simple types are those constructed starting from base types using the
function type constructor; the main difference between these types and
those considered in the earlier walkthrough is that the language of
types now does not include type variables. At the level of terms,
another difference is that the atoms are assumed to have specific
types at the beginning. We are interested in terms that are
constructed only using applications and abstractions and so the type
for atoms (that are only the abstracted variables) is given by
including a type with each abstraction. Typing rules allow us to
assign types to more complicated expressions. These rules look very
much like the ones in the beginner's walkthrough with the only
difference that the rule for abstractions has to use the provided type
for the abstracted variable. The property that we will prove is that
in such a case there is exactly one type that can be assigned to each
well-formed term.
</p>
</div>

<div class="section">
<h1>Executable Specification</h1>

<p>
This walkthrough uses the following signature which we assume is saved
in <code>type-uniq.sig</code>.
</p>

<pre>
sig type-uniq.

kind    tm, ty     type.

type    i          ty.
type    arrow      ty -> ty -> ty.

type    app        tm -> tm -> tm.
type    abs        ty -> (tm -> tm) -> tm.

type    of         tm -> ty -> o.
</pre>

<p>
Note that this signature differs from signature in the first
walkthrough in that the <code>abs</code> constructor now takes two
arguments, the first of which is the type of the variable which is
abstracted over.
</p>

<p>
The specification of typing in the simply-typed lambda-calculus are
specified in the following module which we assume is saved
in <code>type-uniq.mod</code>.
</p>

<pre>
module type-uniq.

of (abs T R) (arrow T U) :- pi x\ (of x T => of (R x) U).
of (app M N) T :- of M (arrow U T), of N U.
</pre>

<p>
Note that the rule for typing abstractions uses the type annotation
which is attached to each abstraction.
</p>
</div>

<div class="section">
<h1>Reasoning</h1>

<p>
The specification is already included with Abella in the directory
<code>examples/lambda-calculus/type-uniq</code> as the
files <code>type-uniq.sig</code> and <code>type-uniq.mod</code>. We
can load this specification in Abella as follows.

<pre>
~/abella/examples/lambda-calculus/type-uniq % <b>abella</b>
Welcome to Abella 2.0.0
Abella < <b>Specification "type-uniq".</b>
Reading specification type-uniq

Abella <
</pre>

We state our type uniqueness theorem as follows.

<pre>
Abella < <b>Theorem type_uniq : forall E T1 T2,
           {of E T1} -> {of E T2} -> T1 = T2.</b>
</pre>

We will induct on the height of <code>{of E T1}</code>,
use <code>intros</code>, and then do case analysis on <code>{of E
T1}</code>. This results in two subgoals corresponding to the two
specification rules for <code>of</code>. The first subgoal is
when <code>E</code> is an abstraction; the second subgoal is
abbreviated for now.

<pre>
Subgoal 1:

  Variables: E, T1, T2, U, R, T
  IH : forall E T1 T2, {of E T1}* -> {of E T2} -> T1 = T2
  H2 : {of (abs T R) T2}
  H3 : {of n1 T |- of (R n1) U}*
  ============================
   arrow T U = T2

Subgoal 2 is:
 T1 = T2
</pre>

At this point <code>E</code> has been bound to <code>(abs T R)</code>.
There is only one way in which <code>{of (abs T R) T2}</code> can be
derived, thus case analysis on this hypothesis acts like unfolding
this judgment. The result is as follows.

<pre>
Subgoal 1:

  Variables: E, T1, T2, U, R, T, U1
  IH : forall E T1 T2, {of E T1}* -> {of E T2} -> T1 = T2
  H3 : {of n1 T |- of (R n1) U}*
  H4 : {of n1 T |- of (R n1) U1}
  ============================
   arrow T U = arrow T U1

Subgoal 2 is:
 T1 = T2
</pre>

It looks like we are almost there, but something goes wrong when we
try to apply the inductive hypothesis to <code>{of n1 T |- of (R n1)
U}*</code> and <code>{of n1 T |- of (R n1) U1}</code>.

<pre>
type_uniq < <b>apply IH to H3 H4.</b>
Error:  Contexts did not match
</pre>

<p>
Basically what happened is that our theorem was stated for typing
judgments that have an empty context of hypothetical assumptions. We
can think of <code>{of E T1}</code> as being
<code>{nil |- of E T1}</code>. When we tried to apply our inductive
hypothesis to judgments which had non-empty contexts, it failed. The
problem is that <em>we need to generalize our theorem over the
possible contexts in which typing judgments are made</em>. Roughly we
want something like the following.

<blockquote>
<code>
forall L E T1 T2, {L |- of E T1} -> {L |- of E T2} -> T1 = T2.
</code>
</blockquote>

However, this statement is too general and, in fact, not true. To begin
with, we are not interested in it being true for all <code>L</code>,
only for those <code>L</code>s that assign types to terms. Moreover,
the relevant <code>L</code>s should assign types only to nominal
constants (which represent abstracted variables in the proof) and at
most one type to each such constant. Notice that without this
uniqueness assumption about the assignment of types to variables, the
proposition cannot be true.
</p>

<p>
The way we will realize this restriction is by defining a property
called <code>ctx</code> that we expect all good contexts to satisfy
and then requiring this property to hold of <code>L</code> in the
statement of the theorem.
If you are following this walkthrough in Abella, you first need to
type "<code>abort.</code>" to give up on our previous proof
attempt. Then enter the following definition of <code>ctx</code>.
</p>

<pre>
Define ctx : olist -> prop by
  ctx nil ;
  nabla x, ctx (of x T :: L) := ctx L.
</pre>

<p>
The system should accept this command with no output. What this
definition says is that the empty list is well-formed context and the
list <code>(of x T :: L)</code> is a well-formed context if
<code>x</code> is a nominal constant and <code>L</code> is a
well-formed context; here <code>nil</code> represents the empty list
and <code>::</code> is a built-in infix constructor for lists.
When we define <code>ctx</code> here we also give it a type which
is <code>olist -> prop</code>, where <code>olist</code> is the type of
lists of terms of type <code>o</code> and <code>prop</code>
is the type of formulas in the reasoning logic.
</p>

<p>
The definition of <code>ctx</code> ensures that the following atomic
formula is provable in the reasoning logic
</p>

<blockquote><code>
ctx (of n1 T1 :: of n2 T2 :: of n3 T3 :: nil)
</code></blockquote>

where <code>n1</code>, <code>n2</code>, and <code>n3</code> are
nominal constants. This definition also ensures that the following
formulas are <em>not</em> provable.

<blockquote><code>
ctx (of n1 T1 :: of n1 T2 :: nil)
</code></blockquote>
<blockquote><code>
ctx (of (app M N) T :: nil)
</code></blockquote>
</code></blockquote>
<blockquote><code>
ctx (of (abs R) T :: nil)
</code></blockquote>

<p>
These non-examples illustrate facts that we would like to prove about
well-formed contexts. The first is that they cannot contain more than
one typing judgment for the same nominal constant. In proving this
lemma, we will need another fact about the non-occurrence of nominal
constants with a particular kind of scope in a list. This property can
be stated using the predicate <code>member</code> that is known to
Abella via the following definition. <b>Do not type this in, since
this is already defined in Abella</b>.

<pre style="background-color: white">
Define member : o -> olist -> prop by
  member A (A :: L) ;
  member A (B :: L) := member A L.
</pre>

The property we need is stated in the following lemma:

<pre>
Theorem member_nominal_absurd : forall L T, nabla x,
  member (of x T) L -> false.
</pre>

Before we consider a proof of this lemma, it is useful to consider the
following simpler observation that is in some ways related:

<pre>
Theorem non_occurrence : forall L, nabla (x:tm),
  (x = L) -> false.
</pre>

The particular order in which the quantifiers over <code>L</code> and
<code>x</code> are scoped automatically enforces the requirement that
any nominal constant substituted for <code>x</code> cannot appear in
the term substituted for <code>L</code>. Abella understands
this and this theorem is therefore easily proved. In detail, the proof
will follow quickly by using the tactics <code>intros</code> and
<code>case H1</code>.
</p>

<p>
Given that non_occurrence is provable, one might wonder if
member_nominal_absurd does not follow immediately from it: the
property is after all similar in that we are trying to show the
non-occurrence of the nominal constant plugged in for <code>x</code>
in the statement of member_nominal_absurd in the term substituted for
<code>L</code>. Unfortunately, this is not true. <code>L</code> is a
list that is, in a sense, constructed using the definition of
<code>member</code> and we have to factor this non-occurrence property
through that definition.
<p>
Once we have observed this much, the actual proof is easy to perform and
so I'll just show it.

<pre>


  ============================
   forall L T, nabla x, member (of x T) L -> false

member_nominal_absurd < <b>induction on 1.</b>
</pre>
<pre>


  IH : forall L T, nabla x, member (of x T) L * -> false
  ============================
   forall L T, nabla x, member (of x T) L @ -> false

member_nominal_absurd < <b>intros.</b>
</pre>
<pre>

  Variables: L, T
  IH : forall L T, nabla x, member (of x T) L * -> false
  H1 : member (of n1 T) L @
  ============================
   false

member_nominal_absurd < <b>case H1.</b>
</pre>
<pre>

  Variables: L, T, L3, L2
  IH : forall L T, nabla x, member (of x T) L * -> false
  H2 : member (of n1 T) L3 *
  ============================
   false

member_nominal_absurd < <b>apply IH to H2.</b>
Proof completed.
</pre>

The interesting part is when we did <code>case H1</code> we didn't
have to consider the subgoal when <code>(of n1 T)</code> is at
the head of <code>L</code>. Here the fact that the instantiation
of <code>L</code> cannot contain <code>n1</code>, the nominal constant
substituted for <code>x</code>, gets used.
</p>

<p>
If we now attempt to prove the uniqueness property for contexts we
will still run into a small problem: we will encounter terms of the
form <code>T n1</code> where <code>T</code> is a variable of
type <code>ty</code> and <code>n1</code> is a nominal constant of type
<code>tm</code>. Such terms are absurd since it is not possible for a
term of type <code>tm</code> to appear inside a term of
type <code>ty</code>. Abella does not know this, however, since the
user may later add a new constant with type <code>tm -> ty</code>
which would allow such dependencies. In order to let Abella know that
such constants will never be introduced, we tell it that the
types <code>tm</code> and <code>ty</code> are <em>closed</em> using
the following command:
<pre>Close tm, ty.</pre>
</p>

<p>
Now we can now state and prove the uniqueness property for contexts.

<pre>
Theorem member_uniq : forall L E T1 T2,
  ctx L -> member (of E T1) L -> member (of E T2) L -> T1 = T2.
</pre>

This lemma can be proved by induction on any of the hypotheses. We
will induct on <code>member (of E T1) L</code> followed by
<code>intros</code> and case analysis on <code>member (of E T1)
L</code>. This results in two subgoals based on whether <code>(of E
T1)</code> occurs in the head or the tail of <code>L</code>. The proof
state is as follows.

<pre>
Subgoal 1:

  Variables: L, E, T1, T2, L1
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H1 : ctx (of E T1 :: L1)
  H3 : member (of E T2) (of E T1 :: L1)
  ============================
   T1 = T2

Subgoal 2 is:
 T1 = T2
</pre>

The first subgoal is when <code>(of E T1)</code> is at the head of
<code>L</code> which is therefore <code>(of E T1 :: L1)</code>. Next
we do case analysis on <code>member (of E T2) (of E T1 :: L1)</code>
which will replace the current subgoal with two new subgoals, bringing
the total number of subgoals to three. The first subgoal will be
when <code>(of E T2)</code> is at the head of the list. This
means <code>T1 = T2</code> so the subgoal is trivially handled
by <code>search.</code> After this, two subgoals remain.

<pre>
Subgoal 1.2:

  Variables: L, E, T1, T2, L1
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H1 : ctx (of E T1 :: L1)
  H4 : member (of E T2) L1
  ============================
   T1 = T2

Subgoal 2 is:
 T1 = T2
</pre>

Let's stop to think about the first subgoal. It is stating
that <code>(of E T1 :: L1)</code> is a well-formed typing context and
yet there is another binding for <code>E</code> in <code>L1</code>.
This is a contradiction and we need to expose it to the prover.

</p>
<p>
Here we perform case analysis on <code>ctx (of E T1 :: L1)</code>.
This case analysis will be interesting because the <code>ctx</code>
was defined with a nabla quantifier in the head of the definition. As
a reminder, here is the relevant definition of <code>ctx</code>.
<b>Don't type this in again, it is only to remind you.</b>

<pre style="background-color: white">
  nabla x, ctx (of x T :: L) := ctx L
</pre>

Based on the intended meaning of this clause that we discussed
earlier, <code>ctx (of E T1 ::
L1)</code> can follow from it only if <code>E</code> is a nominal
constant and <code>T1</code> and <code>L1</code> are restricted to be
terms that do not contain this nominal constant. The logical
form of the unfolding rule is treated in detail in <a
href="http://arxiv.org/pdf/0802.0865v1.pdf">this paper</a>. The
effect of this rule on the existing proof state will be to introduce a
new nominal constant for <code>E</code> and to require that the newly
introduced eigenvariables not contain this constant. Of course, the
previously available eigenvariables can use the nominal constant. To
resolve this seemingly contradictory requirements, we stipulate that
no eigenvariable can be instantiated by a term containing a nominal
constant but we also replace the original eigenvariables by new ones
applied to the introduced nominal constant. This mechanism, that is
known technically as raising, allows dependencies to be represented
even under the restriction on substitutions for eigenvariables. In the
current case, however, the nominal constant we are raising over has
type <code>tm</code> and the only eigenvariables we might have to
raise are of type <code>ty</code>. Since we used
the <code>Close</code> command, Abella knows that the <code>ty</code>
variables cannot depend on terms of type <code>tm</code>, and thus no
raising is performed.
</p>

<p>
Effecting an unfolding in the manner described in the current
situation yields the following as the new proof state.
<pre>
member_uniq < <b>case H1.</b>
Subgoal 1.2:

  Variables: L, E, T1, T2, L1, L2
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H4 : member (of n1 T2) L2
  H5 : ctx L2
  ============================
   T1 = T2

Subgoal 2 is:
 T1 = T2
</pre>

We can complete this subgoal by applying our lemma about occurrences
of nominal constants.

<pre>
member_uniq < <b>apply member_nominal_absurd to H4.</b>
</pre>

This completes the subgoal and leaves just the following subgoal left
to be proven.

<pre>
Subgoal 2:

  Variables: L, E, T1, T2, L1, B
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H1 : ctx (B :: L1)
  H3 : member (of E T2) (B :: L1)
  H4 : member (of E T1) L1 *
  ============================
   T1 = T2
</pre>

Now we perform case analysis on <code>member (of E T2) (B ::
L1)</code> which generates two subgoals. The first subgoal is
symmetric to the previous subgoal we considered, so I'll leave that for
you. The other subgoal is as follows.

<pre>
Subgoal 2.2:

  Variables: L, E, T1, T2, L1, B
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H1 : ctx (B :: L1)
  H4 : member (of E T1) L1 *
  H5 : member (of E T2) L1
  ============================
   T1 = T2
</pre>

All we need now to apply the inductive hypothesis is a judgment like
<code>ctx L1</code>. We get this by case analysis which triggers
raising of eigenvariables; this raising which is shown in the prover
state below ends up having no effect on the proof itself. The rest of
the proof is, in fact, as follows.

<pre>
member_uniq < <b>case H1.</b>
Subgoal 2.2:

  Variables: L, E, T1, T2, L1, B, L2, T
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H4 : member (of (E n1) T1) L2 *
  H5 : member (of (E n1) T2) L2
  H6 : ctx L2
  ============================
   T1 = T2

member_uniq < <b>apply IH to H6 H4 H5.</b>
</pre>
<pre>
Subgoal 2.2:

  Variables: L, E, T1, T2, L1, B, L2, T
  IH : forall L E T1 T2, ctx L -> member (of E T1) L * ->
         member (of E T2) L -> T1 = T2
  H4 : member (of (E n1) T2) L2 *
  H5 : member (of (E n1) T2) L2
  H6 : ctx L2
  ============================
   T2 = T2

member_uniq < <b>search.</b>
Proof completed.
</pre>

We need another property about the context before we can prove our
generalized theorem. We've shown well-formed contexts cannot have two
typing judgments for the same variable, but we still have to show that
such contexts can only contain typing judgments for nominal
constants. For this we define a notation that a term must be a nominal
constant.

<pre>
Define name : tm -> prop by
  nabla x, name x.
</pre>

Given a variable <code>X</code> and an assumption <code>name X</code>,
<code>X</code> must be a nominal constant by the definition
of <code>name</code>. The structural property about a well-formed
context is basically an inspection of its definition, which is stated as
follows.

<pre>
Theorem ctx_mem : forall L E,
   ctx L -> member E L ->  exists N X, E = of X N /\ name X.
</pre>

The proof of this lemma is straightforward so I'll just show it.

<pre>


  ============================
   forall L E, ctx L -> member E L -> (exists N X, E = of X N /\ name X)

ctx_mem < <b>induction on 2.</b>
</pre>
<a name="26"></a>
<pre>


  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\ name X)
  ============================
   forall L E, ctx L -> member E L @ -> (exists N X, E = of X N /\ name X)

ctx_mem < <b>intros.</b>
</pre>
<a name="27"></a>
<pre>

  Variables: L, E
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H1 : ctx L
  H2 : member E L @
  ============================
   exists N X, E = of X N /\ name X

ctx_mem < <b>case H2.</b>
</pre>
<a name="28"></a>
<pre>
Subgoal 1:

  Variables: L, E, L1
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H1 : ctx (E :: L1)
  ============================
   exists N X, E = of X N /\ name X

Subgoal 2 is:
 exists N X, E = of X N /\ name X

ctx_mem < <b>case H1.</b>
</pre>
<a name="29"></a>
<pre>
Subgoal 1:

  Variables: L, E, L1, L2, T
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H3 : ctx L2
  ============================
   exists N X, of n1 T = of X N /\ name X

Subgoal 2 is:
 exists N X, E = of X N /\ name X

ctx_mem < <b>search.</b>
</pre>
<a name="30"></a>
<pre>
Subgoal 2:

  Variables: L, E, L1, B
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H1 : ctx (B :: L1)
  H3 : member E L1 *
  ============================
   exists N X, E = of X N /\ name X

ctx_mem < <b>case H1.</b>
</pre>
<a name="31"></a>
<pre>
Subgoal 2:

  Variables: L, E, L1, B, L2, T
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H3 : member (E n1) L2 *
  H4 : ctx L2
  ============================
   exists N X, E n1 = of X N /\ name X

ctx_mem < <b>apply IH to H4 H3.</b>
</pre>
<a name="32"></a>
<pre>
Subgoal 2:

  Variables: L, E, L1, B, L2, T, N, X
  IH : forall L E, ctx L -> member E L * -> (exists N X, E = of X N /\
         name X)
  H3 : member (of (X n1) N) L2 *
  H4 : ctx L2
  H5 : name (X n1)
  ============================
   exists N1 X1, of (X n1) N = of X1 N1 /\ name X1

ctx_mem < <b>search.</b>
Proof completed.
</pre>

Notice that at one point we did case analysis on <code>ctx (E :: L1)</code>
which exposes <code>E</code> to be <code>of n1 T</code>. Since <code>n1</code>
is a nominal constant, a <code>search</code> completed the subgoal.
</p>

<p>
Now we can state and prove our generalized theorem.

<pre>
Theorem type_uniq_ext : forall L E T1 T2,
  ctx L -> {L |- of E T1} -> {L |- of E T2} -> T1 = T2.
</pre>

The proof of this lemma is by induction on one of the typing
judgments. The body of the proof consists of considering the possible
ways in which the two typing judgments are derived. Because these
typing judgments have non-empty contexts, we will have to consider the
possibility that, for example, the typing judgment <code>(of E
T1)</code> comes directly from the context <code>L</code>. Thus case
analysis on a hypothesis like <code>{L |- of E T1}</code> will result
in three subgoals.

</p>
<p>

The entire proof is listed below. We pay special attention to
cases where the typing judgment is derived from the context. We will
also pause to introduce a convenient feature of the <code>apply</code>
tactic.

<pre>


  ============================
   forall L E T1 T2, ctx L -> {L |- of E T1} -> {L |- of E T2} -> T1 = T2

type_uniq_ext < <b>induction on 2.</b>
</pre>
<a name="35"></a>
<pre>


  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} -> T1 = T2
  ============================
   forall L E T1 T2, ctx L -> {L |- of E T1}@ -> {L |- of E T2} -> T1 = T2

type_uniq_ext < <b>intros.</b>
</pre>
<a name="36"></a>
<pre>

  Variables: L, E, T1, T2
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H2 : {L |- of E T1}@
  H3 : {L |- of E T2}
  ============================
   T1 = T2

type_uniq_ext < <b>case H2.</b>
</pre>

In the first case, the goal formula <code>of E T1</code> is
derived from the program clause for abstraction.

<a name="37"></a>
<pre>
Subgoal 1:

  Variables: L, E, T1, T2, U, R, T
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H3 : {L |- of (abs T R) T2}
  H4 : {L, of n1 T |- of (R n1) U}*
  ============================
   arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H3.</b>
</pre>
<a name="38"></a>
<pre>
Subgoal 1.1:

  Variables: L, E, T1, T2, U, R, T, U1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L, of n1 T |- of (R n1) U}*
  H5 : {L, of n1 T |- of (R n1) U1}
  ============================
   arrow T U = arrow T U1

Subgoal 1.2 is:
 arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>apply IH to _ H4 H5.</b>
</pre>

We are now considering the case when <code>E</code> is the abstraction
<code>(abs T R)</code> and the typing judgments are formed using the
typing rule for abstractions. Thus the typing context has grown
to include a typing assumption for a new nominal constant
<code>n1</code>. In order to apply our inductive hypothesis to the
new typing judgments for <code>(R n1)</code>, we must ensure that
<code>(of n1 T :: L)</code> is a well-formed context, i.e. that it is
accepted by the definition of <code>ctx</code>. This should be easy
since it is clear that neither <code>T</code> nor <code>L</code> can
depends on <code>n1</code>.

</p>
<p>

One option is to use the <code>assert</code> tactic to explicitly
state <code>ctx (of n1 T :: L)</code>. We could then prove this
assertion using the <code>search</code> tactic. A more convenient
option is to use what is called "apply with unknowns." For this we
simply put a <code>_</code> where we want the system to generate a
hypothesis for us. The prover will then guess what needs be proved for
the unknown hypothesis and attempt to prove it using the
<code>search</code> tactic. If this fails, the current subgoal will be
delayed and the unknown hypothesis will become the current goal. In
our case, <code>ctx (of n1 T :: L)</code> will be solved by
<code>search</code> so there is no extra subgoal generated. We now
return you to the rest of the proof.

<a name="39"></a>
<pre>
Subgoal 1.1:

  Variables: L, E, T1, T2, U, R, T, U1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L, of n1 T |- of (R n1) U1}*
  H5 : {L, of n1 T |- of (R n1) U1}
  ============================
   arrow T U1 = arrow T U1

Subgoal 1.2 is:
 arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>search.</b>
</pre>
<a name="40"></a>
<pre>
Subgoal 1.2:

  Variables: L, E, T1, T2, U, R, T, F
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L, of n1 T |- of (R n1) U}*
  H5 : {L, [F] |- of (abs T R) T2}
  H6 : member F L
  ============================
   arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>apply ctx_mem to H1 H6.</b>
</pre>

At this point we encounter a judgment of the form <code>{L, [F] |- G}
</code> for the first time. Such a judgment is called a backchaining
judgment, which indicates that the goal formula <code>G</code> , which
is <code>of (abs T R) T2</code> in this case, is proved by
backchaining on some formula <code>F</code> in the
context <code>L</code>. The hypothesis <code>member F L</code> is
introduced to show that <code>F</code> is contained in
<code>L</code>.

</p>
<p>

If we perform case analysis on <code>H5</code>, we
will get an error message:

<pre>
type_uniq_ext < <b>case H5.</b>
Error: Cannot perform case-analysis on flexible head
</pre>

Case analysis fails because the structure of <code>F</code> is
unknown.  To make progress we apply the lemma <code>ctx_mem</code> to
reveal <code>F</code>, which is of the form <code>of X N</code>. Then
we perform case analysis on <code>H5</code> which backchains the goal
<code>of (abs T R) T2</code> on the clause <code>of X N</code>, as shown below.

<a name="41"></a>
<pre>
Subgoal 1.2:

  Variables: L, E, T1, T2, U, R, T, F, N, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L, of n1 T |- of (R n1) U}*
  H5 : {L, [of X N] |- of (abs T R) T2}
  H6 : member (of X N) L
  H7 : name X
  ============================
   arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H5.</b>
</pre>
<a name="42"></a>
<pre>
Subgoal 1.2:

  Variables: L, E, T1, T2, U, R, T, F, N, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L, of n1 T |- of (R n1) U}*
  H6 : member (of (abs T R) T2) L
  H7 : name (abs T R)
  ============================
   arrow T U = T2

Subgoal 2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H7.</b>
</pre>

As a result of backchaining, <code>X</code> is unified with <code>abs T R</code>.
Case analysis on <code>H7</code> completed this subgoal because <code>abs T R</code>
is not a nominal constant.

</p>
<p>

The case that <code>E</code> is an application is proved in a similar way.

<a name="43"></a>
<pre>
Subgoal 2:

  Variables: L, E, T1, T2, U, N, M
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H3 : {L |- of (app M N) T2}
  H4 : {L |- of M (arrow U T1)}*
  H5 : {L |- of N U}*
  ============================
   T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H3.</b>
</pre>
<a name="44"></a>
<pre>
Subgoal 2.1:

  Variables: L, E, T1, T2, U, N, M, U1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L |- of M (arrow U T1)}*
  H5 : {L |- of N U}*
  H6 : {L |- of M (arrow U1 T2)}
  H7 : {L |- of N U1}
  ============================
   T1 = T2

Subgoal 2.2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>apply IH to H1 H4 H6.</b>
</pre>
<a name="45"></a>
<pre>
Subgoal 2.1:

  Variables: L, E, T1, T2, U, N, M, U1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L |- of M (arrow U1 T2)}*
  H5 : {L |- of N U1}*
  H6 : {L |- of M (arrow U1 T2)}
  H7 : {L |- of N U1}
  ============================
   T2 = T2

Subgoal 2.2 is:
 T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>search.</b>
</pre>
<a name="46"></a>
<pre>
Subgoal 2.2:

  Variables: L, E, T1, T2, U, N, M, F
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L |- of M (arrow U T1)}*
  H5 : {L |- of N U}*
  H6 : {L, [F] |- of (app M N) T2}
  H7 : member F L
  ============================
   T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>apply ctx_mem to H1 H7.</b>
</pre>
<a name="47"></a>
<pre>
Subgoal 2.2:

  Variables: L, E, T1, T2, U, N, M, F, N1, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L |- of M (arrow U T1)}*
  H5 : {L |- of N U}*
  H6 : {L, [of X N1] |- of (app M N) T2}
  H7 : member (of X N1) L
  H8 : name X
  ============================
   T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H6.</b>
</pre>
<a name="48"></a>
<pre>
Subgoal 2.2:

  Variables: L, E, T1, T2, U, N, M, F, N1, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H4 : {L |- of M (arrow U T1)}*
  H5 : {L |- of N U}*
  H7 : member (of (app M N) T2) L
  H8 : name (app M N)
  ============================
   T1 = T2

Subgoal 3 is:
 T1 = T2

type_uniq_ext < <b>case H8.</b>
</pre>

The last case is when the goal formula is derived from some clause <code>F</code>
in the context. As described before, we first apply the lemma <code>ctx_mem</code>
to reveal the structure of <code>F</code>. We then perform case analysis to backchain
the goal formula on <code>F</code>.
<a name="49"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H3 : {L |- of E T2}
  H4 : {L, [F] |- of E T1}*
  H5 : member F L
  ============================
   T1 = T2

type_uniq_ext < <b>apply ctx_mem to H1 H5.</b>
</pre>
<a name="50"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H3 : {L |- of E T2}
  H4 : {L, [of X N] |- of E T1}*
  H5 : member (of X N) L
  H6 : name X
  ============================
   T1 = T2

type_uniq_ext < <b>case H4.</b>
</pre>
<a name="51"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx L
  H3 : {L |- of E T2}
  H5 : member (of E T1) L
  H6 : name E
  ============================
   T1 = T2

type_uniq_ext < <b>case H6.</b>
</pre>

The case analysis above reveals the fact that <code>E</code> is a
nominal constant. Eigenvariables are raised over this nominal
constant <code>n1</code> because of their dependency
on <code>n1</code>.

Now the goal formula <code>of n1 T2</code> in
<code>H3</code> can only be derived from the context because the
nominal constant <code>n1</code> cannot be an abstraction or an
application. The same technique for reasoning about backchaining
judgments is used once again for <code>{L n1 |- of n1 T2}</code>.

<a name="52"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx (L n1)
  H3 : {L n1 |- of n1 T2}
  H5 : member (of n1 T1) (L n1)
  ============================
   T1 = T2

type_uniq_ext < <b>case H3.</b>
</pre>
<a name="53"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X, F1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx (L n1)
  H5 : member (of n1 T1) (L n1)
  H7 : {L n1, [F1 n1] |- of n1 T2}
  H8 : member (F1 n1) (L n1)
  ============================
   T1 = T2

type_uniq_ext < <b>apply ctx_mem to H1 H8.</b>
</pre>
<a name="54"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X, F1, N1, X1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx (L n1)
  H5 : member (of n1 T1) (L n1)
  H7 : {L n1, [of (X1 n1) N1] |- of n1 T2}
  H8 : member (of (X1 n1) N1) (L n1)
  H9 : name (X1 n1)
  ============================
   T1 = T2

type_uniq_ext < <b>case H7.</b>
</pre>
<a name="55"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X, F1, N1, X1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx (L n1)
  H5 : member (of n1 T1) (L n1)
  H8 : member (of n1 T2) (L n1)
  H9 : name n1
  ============================
   T1 = T2

type_uniq_ext < <b>apply ctx_uniq to H1 H5 H8.</b>
</pre>

At this point we know <code>of n1 T1</code> and <code>of n1 T2</code>
are both in the same context. By applying <code>ctx_uniq</code> we prove
that <code>T1</code> and <code>T2</code> are the same.

<a name="56"></a>
<pre>
Subgoal 3:

  Variables: L, E, T1, T2, F, N, X, F1, N1, X1
  IH : forall L E T1 T2, ctx L -> {L |- of E T1}* -> {L |- of E T2} ->
         T1 = T2
  H1 : ctx (L n1)
  H5 : member (of n1 T2) (L n1)
  H8 : member (of n1 T2) (L n1)
  H9 : name n1
  ============================
   T2 = T2

type_uniq_ext < <b>search.</b>
Proof completed.
</pre>

We can now use this generalized theorem to prove our specific theorem.
Watch closely and you'll see another use of apply with unknowns.

<pre>
Abella < <b>Theorem type_uniq : forall E T1 T2,
           {of E T1} -> {of E T2} -> T1 = T2.</b>


  ============================
   forall E T1 T2, {of E T1} -> {of E T2} -> T1 = T2

type_uniq < <b>intros.</b>
</pre>
<pre>

  Variables: E, T1, T2
  H1 : {of E T1}
  H2 : {of E T2}
  ============================
   T1 = T2

type_uniq < <b>apply type_uniq_ext to _ H1 H2.</b>
</pre>
<pre>

  Variables: E, T1, T2
  H1 : {of E T2}
  H2 : {of E T2}
  ============================
   T2 = T2

type_uniq < <b>search.</b>
Proof completed.
</pre>


Congratulations! We've now proved type uniqueness for the simply-typed
lambda-calculus. You can look
at <a href="examples/lambda-calculus/type-uniq/type-uniq.html">
examples/lambda-calculus/type-uniq/type-uniq.thm</a> for a full
development.
</p>

<p>
You should now have a basic understanding of the most popular tactics
in Abella as well as of definitions and lemmas. With this knowledge
you are ready to understand any of the examples included with Abella.
I suggest looking at the examples listed on
the <a href="index.html">homepage</a> and perhaps those smaller
examples listed <a href="examples/">here</a>. You are also encourage
to specify and reason about your own logical system. If so, please let
us hear about
it:
<a href="http://groups.google.com/group/abella-theorem-prover">Abella
    mailing list</a>
</p>

</div>

</body>
</html>

<!--  LocalWords:  uniq IH forall ctx nabla Abella Walkthrough walkthrough sig
 -->
<!--  LocalWords:  abella subgoals subgoal eigenvariables eigenvariable ty
 -->
<!--  LocalWords:  olist
 -->
